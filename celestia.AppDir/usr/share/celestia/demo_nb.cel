{
	timerate { rate 0 }
	labels { clear "planets|minorplanets|stars|constellations" }
	renderflags { set "stars|planets"
                      clear "constellations|orbits|cloudmaps" }
	print { text "Begynner demo . . .\nTrykk ESC for å slutte." origin "center" duration 2 }
	wait { duration 2.0 }

	print { text "Vi begynner i nabolaget . . ." row -3 }
	select { object "Sol/Earth" }
	cancel {}
	# goto { time 0 distance 3 upframe "ecliptical" }
	# gotolonglat { time 0 distance 3 longitude -122 latitude 47 }
	synchronous {}
	gotoloc { time 0.0 position [ 0 0 20000 ] }
	wait { duration 0.1 }
	center { time 0.1 }
	wait { duration 1.0 }
	follow {}

	print { text "Vi er nå i bane omtrent 12.500km over Jorden" row -3 duration 5 }
	orbit { axis [ 0 1 0 ] rate 30 duration 10 }
	print { text "Jorden ser mye mer velkjent ut med skyer." row -3}
	wait { duration 0.1 }
	renderflags { set "cloudmaps" }
	orbit { axis [ 0 1 0 ] rate 30 duration 6 }

	print { text "Neste stopp: Månen." row -3 }
	select { object "Moon" }
	goto { time 5 distance 4 upframe "equatorial" }
	wait { duration 5.5 }
	print { text "Se etter Solen og Jorden mens vi går i bane rundt Månen" row -3}
	orbit { axis [ 0 1 0 ] rate 30 duration 10 }
	
	print { text "Så reiser vi mot Solen." row -3}
	select { object "Sol" }
	goto { time 8 distance 12 upframe "equatorial" up [ 0 1 0 ] }
	wait { duration 8.5 }
	print { text "På denne avstanden kan vi se mørke solflekker på Solens overflate." row -3}
	orbit { axis [ 0 1 0 ] rate 20 duration 10 }

        print { text "Vi trekker oss ut for å få en oversikt over det indre solsystemet." row -3}
	orbit { axis [ 1 0 0 ] rate 45 duration 2 }
	renderflags { set "orbits" }
	changedistance { duration 4.0 rate 1.0 }
	
	print { text "Setter etiketter på planetene . . ." row -3}
	labels { set "planets" }
	wait { duration 1.0 }
	print { text "Vi kan øke farten for å se planetene gå i bane rundt Solen." row -3}
	timerate { rate 2592000 }
	wait { duration 3.0 }
	print { text "For hvert sekund går det en måned i simuleringen." row -3}
	wait { duration 12.0 }
	timerate { rate 1 }
	print { text "Nå har tiden stoppet helt." row -3}

	wait { duration 1.0 }
	print { text "Vårt neste mål er Saturn." row -3}
	select { object "Saturn" }
	center { time 2 }
	wait { duration 2 }
	goto { time 8 distance 6 up [ 0 1 0 ] upframe "equatorial" }
	wait { duration 6.5 }
	renderflags { clear "orbits" }
	labels { clear "planets" }
	print { text "Flere av Saturns måner er synlige som lyse punkter." row -3 duration 3}
	orbit { axis [ 0 1 0 ] rate 30 duration 12 }
	
	select { object "Mimas" }
	goto { time 5 distance 4 upframe "equatorial" }
	print { text "Det tydeligste trekket på Saturns måne Mimas er det enorme nedslagskrateret Herschel." row -3 duration 9 }
	orbit { axis [ 0 1 0 ] rate 30 duration 12 }
	changedistance { duration 6.0 rate 0.5 }

	select { object "Sol" }
	center { time 2 }
	print { text "Legg merke til hvor svak Solen ser ut på denne avstanden." row -3 
}
	wait { duration 2 }

	print { text "La oss se oss om på stjernehimmelen." row -3 duration 2 }
	wait { duration 2 }
	select { object "Alpha UMa" }
	center { time 2 }
	print { text "De som bor på den nordlige halvkulen kjenner igjen Karlsvogna i stjernebildet Store Bjørn." row -3 duration 3 }
	wait { duration 4 }

	select { object "Polaris" }
	center { time 2 }
	wait { duration 2 }
	print { text "Nå ser vi rett på Polaris, Nordstjernen." row -3}
	wait { duration 1 }
	labels { set "stars" }
	wait { duration 2 }
	print { text "Polaris er en del av stjernebildet Lille Bjørn." row -3}
	wait { duration 2 }
	print { text "Celestia kan tegne stjernebildediagrammer så vi kan lettere finne fram . . ." row -3}
	renderflags { set "constellations" }
	wait { duration 2 }
	print {". . . og stjernebildenavn." row -3}
	labels { set "constellations" }
	wait { duration 2 }

	select { object "Alnilam" }
	center { time 4 }
	wait { duration 2 }
	print { text "Orion er det tydeligste stjernebildet på himmelen." row -3 duration 3 }
	wait { duration 4 }

	select { object "Beta Cru" }
	center { time 4 }
	wait { duration 2 }
	print { text "Sydkorset er et velkjent stjernebilde på den sørlige halvkulen." row -3 duration 4 }
	wait { duration 4 }

	rotate { axis [ 0.707 0.707 0 ] rate 20 duration 7 }
	print { text "Nå slår vi på galakseopptegning så vi kan se Melkeveien" row -3 duration 4 }
	renderflags { set "galaxies" }
	rotate { axis [ 0.707 0.707 0 ] rate 20 duration 14 }
	rotate { axis [ 0.707 0.707 0 ] rate 20 duration 10 }

	select { object "Antares" }
	center { time 5 }
	wait { duration 3 }
	print { text "Nå reiser vi til Antares, en rød kjempestjerne i Skorpionen." row -3 duration 5 }
	wait { duration 2 }
	renderflags { clear "constellations" }
	labels { clear "constellations|stars" }
	wait { duration 1 }
	goto { time 8 distance 200 }
	wait { duration 8.5 }
	goto { time 5 distance 10 }
	wait { duration 5.0 }
	print { text "Selv om vi nå er 10 ganger lenger unna Antares enn\nJorden er fra Solen, så 
tar den enorme røde stjernen fremdeles stor på himmelen." row -3}
	wait { duration 4.0 }

	print { text "Vi trekker oss ut for å se det store bildet . . ." row -3}
	changedistance { duration 10.0 rate 2.0 }

	select { object "Milky Way" }
	print { text "Nå ser vi hele Melkeveigalaksen." row -3 duration 6 }
	orbit { axis [ 1 0 0 ] rate 30 duration 16.0 }
	print { text "På tide å dra hjem . . ." row -3}

	select { object "Sol/Earth" }
	goto { time 20 distance 10 upframe "equatorial" }
	wait { duration 20.0 }
	print { text "Demo fullført." row -3}
	wait { duration 3.0 }
}
